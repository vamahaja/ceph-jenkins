pipeline {
    agent {
        node {
            label "ceph-build-centos9-agent"
        }
    }

    options {
        // Keep the last 20 builds
        buildDiscarder(logRotator(numToKeepStr: "20"))

        // Do not checkout code automatically
        skipDefaultCheckout()

        // Timestamps in console output
        timestamps()

        // Colorize console output
        ansiColor("xterm")
    }

    environment {
        // Environment & Variable Setup
        REPO_URL="https://shaman.ceph.com/r/ceph"

        // Set user build parameters
        RPM_BUILD_OPTS="${params.RPM_BUILD_OPTS}"

        // Set up a build paths for artifacts
        RPM_BUILDAREA="${WORKSPACE}/rpm"
        RELEASE_BUILDAREA="${WORKSPACE}/release"
        CEPH_DIR="${WORKSPACE}/ceph"
        BUILD_BINARY="${WORKSPACE}/jobs/pipelines/sample_ceph_build_ci/build_binary.sh"
    }

    stages {
        stage("Checkout Pipeline Code") {
            steps {
                // Pull the Jenkinsfile repository
                checkout scm
            }
        }

        stage("Checkout Ceph Repository") {
            steps {
                echo "Checking out Ceph source code ..."

                // Pull Ceph source code
                dir("${CEPH_DIR}") {
                    script {
                        echo "Updating existing repository and submodules ..."

                        // Checkout is blocking: clone + submodules complete before next step
                        checkout([
                            $class: "GitSCM",
                            branches: [[name: "${params.CEPH_BRANCH}"]],
                            extensions: [
                                [$class: "CleanBeforeCheckout",
                                    deleteUntrackedNestedRepositories: true],
                                [$class: "CloneOption", depth: 1, shallow: true,
                                    noTags: false],
                                [$class: "SubmoduleOption",
                                    disableSubmodules: false,
                                    parentCredentials: true,
                                    recursiveSubmodules: true,
                                    shallow: true
                                ]
                            ],
                            userRemoteConfigs: [[url: "${params.CEPH_REPO}"]]
                        ])

                        // Ensure submodules are fully checked out before proceeding
                        sh(
                            script: "git submodule status --recursive",
                            label: "Verify submodules"
                        )

                        // fetch all tags
                        sh(
                            script: "git fetch --tags --unshallow || git fetch --tags",
                            label: "Fetch all tags"
                        )

                        // Verify commit hash and version
                        env.SHA1 = sh(
                            script: "git rev-parse HEAD 2>/dev/null || echo \"unknown\"",
                            returnStdout: true
                        ).trim()
                        env.VERSION = sh(
                            script: "git describe --abbrev=8 --match \"v*\" --always " +
                                "| sed \"s/^v//\"",
                            returnStdout: true
                        ).trim()

                        // Replace last '-' with '.' in VERSION
                        env.VERSION = sh(
                            script: "echo $VERSION | sed \"s/-/./2\"", returnStdout: true
                        ).trim()
                    }
                }
            }
        }

        stage("Prepare Ceph Build Environment") {
            steps {
                echo "Preparing the Ceph build environment ..."

                // Run setup_rpm script to prepare the build environment
                dir("${CEPH_DIR}") {
                    script {
                        // Set distro version based on distro
                        if ("centos9" in params.DISTRO) {
                            env.DISTRO_VERSION = "9"
                            env.BUILD_RPM = "${WORKSPACE}/containers/agents/build/" +
                                "centos-9/build_rpm"
                        } else {
                            error "Unsupported distro: ${params.DISTRO}"
                        }

                        // Set repo url for shaman
                        env.REPO_URL = "${REPO_URL}/${params.CEPH_BRANCH}/${SHA1}/" +
                            "${params.DISTRO}/${DISTRO_VERSION}/flavors/${params.FLAVOR}"
                    }
                }
            }
        }

        stage("Build Binary") {
            steps {
                echo "Running the Ceph binary build script ..."

                dir("${CEPH_DIR}") {
                    script {
                        // Run build_binary script
                        sh "chmod +x ${BUILD_BINARY} && ${BUILD_BINARY}"
                    }
                }
            }
        }

        stage("Build RPMs") {
            steps {
                echo "Running the Ceph RPM build script ..."

                // Run build_rpm script
                dir("${CEPH_DIR}") {
                    script {
                        sh "chmod +x ${BUILD_RPM} && ${BUILD_RPM}"
                    }
                }
            }
        }
    }

    post {
        success {
            echo "RPMs built successfully! Archiving ..."
            script {
                // Save the RPMs
                dir("artifacts") {
                    script {
                        // Create the directories
                        sh "mkdir -p x86_64 noarch SRPMS"

                        // Copy the RPMs
                        sh """
                           find ${RPM_BUILDAREA} -name "*x86_64.rpm" -exec cp {} x86_64/ \\;
                           find ${RPM_BUILDAREA} -name "*noarch.rpm" -exec cp {} noarch/ \\;
                           find ${RPM_BUILDAREA} -name "*src.rpm" -exec cp {} SRPMS/ \\;
                        """

                        // Archive the RPMs
                        archiveArtifacts(
                            artifacts: "x86_64/*.rpm,noarch/*.rpm,SRPMS/*.rpm",
                            allowEmptyArchive: false
                        )
                    }
                }
            }
        }

        failure {
            echo "Build failed. Check the logs for GCC or CMake errors."
        }
    }
}